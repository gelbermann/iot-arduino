#include <Servo.h>
#include <SoftwareSerial.h>
#include <PN532_SWHSU.h>
#include <PN532.h>

/* -------------------------------------------------------------------------- */
/*                                    setup                                   */
/* -------------------------------------------------------------------------- */

/* --------------------------------- globals -------------------------------- */

#define MIN_DIST 10
#define MAX_DIST 2000
#define BAUD 115200

// Distance sensor globals
#define echoPin 2 // attach pin D2 Arduino to pin Echo of HC-SR04
#define trigPin 3 //attach pin D3 Arduino to pin Trig of HC-SR04
long duration;    // variable for the duration of sound wave travel
int distance;     // variable for the distance measurement

// NFC globals
SoftwareSerial SWSerial(10, 11); // RX, TX
PN532_SWHSU pn532swhsu(SWSerial);
PN532 nfc(pn532swhsu);

// Locks globals
Servo servo;

enum e_state
{
    NO_INPUT,
    NFC_DETECTED,
    NFC_DOOR_OPEN,
};
enum e_state STATE = NO_INPUT;

/* ----------------------------- setup functions ---------------------------- */

void setup_distance_sensor()
{
    pinMode(trigPin, OUTPUT);                         // Sets the trigPin as an OUTPUT
    pinMode(echoPin, INPUT);                          // Sets the echoPin as an INPUT
    Serial.println("Ultrasonic Sensor HC-SR04 Test"); // print some text in Serial Monitor
    Serial.println("with Arduino UNO R3");
}

void setup_nfc()
{
    Serial.println("Hello Maker!");
    nfc.begin();
    uint32_t versiondata = nfc.getFirmwareVersion();

    if (!versiondata)
    {
        Serial.print("Didn't Find PN53x Module");
        while (1)
            ; // Halt
    }

    // Got valid data, print it out!

    Serial.print("Found chip PN5");
    Serial.println((versiondata >> 24) & 0xFF, HEX);

    Serial.print("Firmware ver. ");
    Serial.print((versiondata >> 16) & 0xFF, DEC);

    Serial.print('.');
    Serial.println((versiondata >> 8) & 0xFF, DEC);

    // Configure board to read RFID tags

    nfc.SAMConfig();

    Serial.println("Waiting for an ISO14443A Card ...");
}

/* -------------------------------------------------------------------------- */
/*                                  functions                                 */
/* -------------------------------------------------------------------------- */

void open_lock() // TODO open SPECIFIC lock
{
    Serial.println("Opening lock");
    servo.write(90);
}

int get_door_distance_from_sensor()
{
    // Clears the trigPin condition
    digitalWrite(trigPin, LOW);
    delayMicroseconds(2);

    // Sets the trigPin HIGH (ACTIVE) for 10 microseconds
    digitalWrite(trigPin, HIGH);
    delayMicroseconds(10);
    digitalWrite(trigPin, LOW);

    // Reads the echoPin, returns the sound wave travel time in microseconds
    duration = pulseIn(echoPin, HIGH);

    // Calculating the distance
    distance = duration * 0.034 / 2; // Speed of sound wave divided by 2 (go and back)

    // Displays the distance on the Serial Monitor
    Serial.print("Distance: ");
    Serial.print(distance);
    Serial.println(" cm");
    return distance;
}

void listen_for_nfc()
{
    boolean success;
    uint8_t sensor_dist_from_door;

    if (STATE == NO_INPUT)
    {
        success = check_nfc();
        if (success)
        {
            STATE = NFC_DETECTED;
            Serial.println("switching to NFC_DETECTED");
            open_lock();
        }
    }

    else if (STATE == NFC_DETECTED)
    {
        sensor_dist_from_door = get_door_distance_from_sensor();
        if (sensor_dist_from_door >= MIN_DIST)
        {
            open_lock();
            STATE = NFC_DOOR_OPEN;
            delay(2000);
            Serial.println("switching to NFC_DOOR_OPEN");
        }
    }
    else if (STATE == NFC_DOOR_OPEN && nfc_close_lock())
    {
        STATE = NO_INPUT;
        Serial.println("switching to NO_INPUT");
    }
}

boolean check_nfc()
{
    boolean success;
    uint8_t uid[] = {0, 0, 0, 0, 0, 0, 0}; // Buffer to store the returned UID
    uint8_t uidLength;                     // Length of the UID (4 or 7 bytes depending on ISO14443A card type)

    success = nfc.readPassiveTargetID(PN532_MIFARE_ISO14443A, &uid[0], &uidLength);
    if (success)
    {
        // TODO: verify nfc results against webserver, get locker information for webserver

        Serial.println("Found A Card!");

        Serial.print("UID Length: ");
        Serial.print(uidLength, DEC);
        Serial.println(" bytes");

        Serial.print("UID Value: ");
        for (uint8_t i = 0; i < uidLength; i++)
        {
            Serial.print(" 0x");
            Serial.print(uid[i], HEX);
        }

        Serial.println("");
        delay(500);
    }
    else
    {
        // PN532 probably timed out waiting for a card
        // Serial.println("Timed out! Waiting for a card...");
    }
    return success;
}

boolean nfc_close_lock()
{
    if (servo.read() != 0 && distance_okay_to_close_lock())
    {
        Serial.println("Closing lock");
        servo.write(0);
        return true;
    }
    return false;
}

boolean distance_okay_to_close_lock()
{
    uint8_t res1 = get_door_distance_from_sensor();
    uint8_t res2 = get_door_distance_from_sensor();
    uint8_t res3 = get_door_distance_from_sensor();

    // empirical heuristic for distance sensor sensitivity
    return (res1 < MIN_DIST && res2 < MIN_DIST && res3 < MIN_DIST);
}

boolean distance_okay_to_open_lock()
{
    uint8_t res1 = get_door_distance_from_sensor();
    uint8_t res2 = get_door_distance_from_sensor();
    uint8_t res3 = get_door_distance_from_sensor();

    // empirical heuristic for distance sensor sensitivity
    return (res1 >= MIN_DIST && res2 >= MIN_DIST && res3 >= MIN_DIST) && (res1 < MAX_DIST && res2 < MAX_DIST && res3 < MAX_DIST);
}

void listen_for_esp32()
{
    // TODO
}

void listen_for_webserver_for_deposit()
{
    // TODO
}

/* -------------------------------------------------------------------------- */
/*                              arduino template                              */
/* -------------------------------------------------------------------------- */

void setup()
{
    Serial.begin(BAUD);

    // put your setup code here, to run once:
    servo.attach(8);
    servo.write(0);
    delay(2000);

    setup_nfc();
    setup_distance_sensor();
}

void loop()
{
    Serial.print(STATE, DEC);
    Serial.println("");
    listen_for_nfc();
    listen_for_esp32();
    listen_for_webserver_for_deposit();
}